buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}


plugins {
    id 'io.codearte.nexus-staging' version '0.11.0'
}

apply plugin: 'com.bmuschko.nexus'
apply plugin: 'java-library'
apply plugin: 'jacoco'


archivesBaseName = 'paddle-verifier'
group = "com.jamiussiam"
version = "1.0"

modifyPom {
    //noinspection GroovyAssignabilityCheck
    project {
        name 'paddle-verifier'
        description 'A Paddle.com webhook verification library for Java.'
        url 'https://github.com/jamius19/paddle-verifier'
        inceptionYear '2020'

        scm {
            url 'https://github.com/jamius19/paddle-verifier.git'
            connection 'scm:https://github.com/jamius19/paddle-verifier.git'
            developerConnection 'scm:git:git@github.com:jamius19/paddle-verifier.git'
        }

        licenses {
            license {
                name 'MIT License'
                url 'https://opensource.org/licenses/MIT'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'jamius19'
                name 'Jamius Siam'
                email 'jamiusiam@gmail.com'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = true
    javadoc = true
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}

nexusStaging {
    packageGroup = "com.jamiussiam" //optional if packageGroup == project.getGroup()
    stagingProfileId = "jamius19" //when not defined will be got from server using "packageGroup"
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.bouncycastle:bcprov-jdk15on:1.65"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.0"
}


test {
    useJUnitPlatform()
}



jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

check.dependsOn jacocoTestReport



tasks.test {
    finalizedBy(tasks.jacocoTestReport)
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}